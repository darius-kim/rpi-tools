#
# qmake configuration for building with arm-none-linux-gnueabi-g++
#

include(../../common/linux.conf)
include(../../common/gcc-base-unix.conf)
include(../../common/g++-unix.conf)
include(../../common/qws.conf)

RPI_SYSROOT             = $$(HOME)/Library/rootfs_wheezy
TOOL_CHAIN_PATH         = $$(HOME)/x-tools/arm-rpi-linux-gnueabihf/bin
CROSS_COMPILE           = $${TOOL_CHAIN_PATH}/arm-rpi-linux-gnueabihf-
GCC_MACHINE_DUMP        = arm-linux-gnueabihf

QMAKE_INCDIR_QT         = $${RPI_SYSROOT}/usr/include/qt4
QMAKE_LIBDIR_QT         = $${RPI_SYSROOT}/usr/lib/$${GCC_MACHINE_DUMP}

# modifications to g++.conf
QMAKE_CC                = $${CROSS_COMPILE}gcc
QMAKE_CXX               = $${CROSS_COMPILE}g++
QMAKE_LINK              = $${CROSS_COMPILE}g++
QMAKE_LINK_SHLIB        = $${CROSS_COMPILE}g++

# modifications to linux.conf
QMAKE_AR                = $${CROSS_COMPILE}ar cqs
QMAKE_OBJCOPY           = $${CROSS_COMPILE}objcopy
QMAKE_STRIP             = $${CROSS_COMPILE}strip

QMAKE_LFLAGS           += -Wl,-rpath,/opt/vc/lib \
                          -Wl,-rpath-link,$${RPI_SYSROOT}/opt/vc/lib

QMAKE_CFLAGS           += -DQT_OPENGL_ES_2
QMAKE_CFLAGS           += -Wno-psabi \
                          -marm \
                          -mfpu=vfp \
                          -mtune=arm1176jzf-s \
                          -march=armv6zk \
                          -mabi=aapcs-linux
                          
QMAKE_CXXFLAGS          = -std=c++11 $$QMAKE_CFLAGS

QMAKE_LIBDIR           += $${RPI_SYSROOT}/usr/lib/$${GCC_MACHINE_DUMP}
QMAKE_LFLAGS           += -Wl,-rpath-link,$${RPI_SYSROOT}/usr/lib/$${GCC_MACHINE_DUMP} \
                          -Wl,-rpath-link,$${RPI_SYSROOT}/lib/$${GCC_MACHINE_DUMP}
QMAKE_LFLAGS           += -Wl,-rpath-link,$${RPI_SYSROOT}/lib \
                          -Wl,-rpath-link,$${RPI_SYSROOT}/usr/lib

# opengl
QMAKE_INCDIR_OPENGL     = $${RPI_SYSROOT}/opt/vc/include \
                          $${RPI_SYSROOT}/opt/v6c/include/interface/vcos/pthreads \
                          $${RPI_SYSROOT}/opt/vc/include/interface/vmcs_host/linux
QMAKE_LIBDIR_OPENGL     = $${RPI_SYSROOT}/opt/vc/lib
QMAKE_INCDIR_OPENGL_ES1 = $$QMAKE_INCDIR_OPENGL
QMAKE_LIBDIR_OPENGL_ES1 = $$QMAKE_LIBDIR_OPENGL
QMAKE_INCDIR_OPENGL_ES2 = $$QMAKE_INCDIR_OPENGL
QMAKE_LIBDIR_OPENGL_ES2 = $$QMAKE_LIBDIR_OPENGL

QMAKE_LIBS_EGL          = -lEGL -lGLESv2
QMAKE_LIBS_OPENGL       = $${QMAKE_LIBS_EGL}
QMAKE_LIBS_OPENGL_QT    = $${QMAKE_LIBS_EGL}
QMAKE_LIBS_OPENGL_ES1   = 
QMAKE_LIBS_OPENGL_ES2   = $${QMAKE_LIBS_OPENGL}
                    
PKG_CONFIG_SYSROOT_DIR = $${RPI_SYSROOT}
PKG_CONFIG_LIBDIR = $${RPI_SYSROOT}/usr/lib/pkgconfig
PKG_CONFIG_LIBDIR = $${PKG_CONFIG_LIBDIR}:$${RPI_SYSROOT}/usr/lib/$${GCC_MACHINE_DUMP}/pkgconfig
PKG_CONFIG_LIBDIR = $${PKG_CONFIG_LIBDIR}:$${RPI_SYSROOT}/usr/share/pkgconfig
PKG_CONFIG = pkg-config
PKG_CONFIG = PKG_CONFIG_SYSROOT_DIR=\"$$PKG_CONFIG_SYSROOT_DIR\" $$PKG_CONFIG
PKG_CONFIG = PKG_CONFIG_LIBDIR=\"$$PKG_CONFIG_LIBDIR\" $$PKG_CONFIG

load(qt_config)
